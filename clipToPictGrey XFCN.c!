function clipToPictGrey rNam
  if rNam = "?" then
    return "put|get clipToPictGrey(<name>)"
  else if rNam = "!" then
    return "clipToPictGrey 1.0, © 1996 by Uli Kusterer, all rights reserved."
  end if
  put newHandle(0) into myPict
  put getScrap(myPict,"PICT",theOffs) into theLen
  if theLen = -102 then return "Error: Clipboard is no picture."
  if theLen < 0 then return "Error: Couldn't get picture."
  put newPtr(8) into picRect
  put myPict@@.integerType[2] into picRect@.integerType[1]
  put myPict@@.integerType[3] into picRect@.integerType[2]
  put myPict@@.integerType[4] into picRect@.integerType[3]
  put myPict@@.integerType[5] into picRect@.integerType[4]
  put openPicture(picRect@) into myGraph
  drawPicture myPict,picRect@
  penPat gray
  penMode notPatBic
  paintRect picRect@
  penMode patCopy
  penPat black
  closePicture
  disposPtr picRect
  disposHandle myPict
  put get1NamedResource("PICT",rNam) into myRes
  if myRes ≠ NIL then
    rmveResource myRes
  end if
  put uniqueID("PICT") into theID
  AddResource myGraph,"PICT",theID,rNam
  if resError() ≠ 0 then
    return "Error: Unknown #" & resError()
  end if
  writeResource myGraph
  return theID
end clipToPictGrey