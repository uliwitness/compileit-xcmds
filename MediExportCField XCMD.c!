on MediExportCField theID,resID,slideName,fPath
  if theID = "?" then
    return "MediExportCField <fldID>,<resID>,<slideName>,<filePath>"
  else if theID = "!" then
    return "MediExportCField 1.1, © 1995-97 by M. Uli Kusterer, all rights reserved."
  end if
  createResFile fPath
  put openResFile(fPath) into myRefNum
  if resError() ≠ 0 then
    return "ERROR: Couldn't open file."
  end if
  useResFile myRefNum
  put getFieldTE(FALSE,theID,0,"") into myTEHandle -- FALSE is bg field
  if myTEHandle = NIL then
    closeResFile myRefNum
    return "Error: Couldn't BKGND get field."
  end if
  put myTEHandle@@.hText into myTxtHand
  put handToHand(myTxtHand) into err
  if err ≠ 0 then
    TEDispose myTEHandle
    closeResFile myRefNum
    return "Error: Couldn't extract Text."
  end if
  put getResource("SLID",resID) into delRes
  if delRes ≠ NIL then
    rmveResource delRes
  end if
  put getNamedResource("SLID",slideName) into delRes
  if delRes ≠ NIL then
    rmveResource delRes
  end if
  AddResource myTxtHand,"SLID",resID,slideName
  if resError() ≠ NIL then
    TEDispose myTEHandle
    closeResFile myRefNum
    disposHandle myTxtHand
    return "Error: Couldn't write Text."
  end if
  writeResource myTxtHand
  releaseResource myTxtHand
  put 0 into myTEHandle@@.selStart
  put myTEHandle@@.teLength into myTEHandle@@.selEnd
  put GetStylScrap(myTEHandle) into myTEStyles
  put getResource("sSTY",resID) into delRes
  if delRes ≠ NIL then
    rmveResource delRes
  end if
  put getNamedResource("sSTY",slideName) into delRes
  if delRes ≠ NIL then
    rmveResource delRes
  end if
  AddResource myTEStyles,"sSTY",resID,slideName
  if resError() ≠ NIL then
    TEDispose myTEHandle
    closeResFile myRefNum
    disposHandle myTxtHand
    disposHandle myTEStyles
    return "Error: Couldn't write styles."
  end if
  writeResource myTEStyles
  releaseResource myTEStyles
  TEDispose myTEHandle
  closeResFile myRefNum
end MediExportCField