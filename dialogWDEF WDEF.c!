CodeResource "WDEF" 128

-- 52428 = Rand
-- 61166 = Titelleiste
-- 43690 = Schließfeld
-- 48059 = GrowBox (obere)
-- 30583 = Highlight

Constant kGrowBoxSize = 16

recordOffset pmBounds:R @ 6

pascal function dialogWDEF :L varCode:I, theWindow:P, nachricht:I, daten:L
  if theWindow@.visible = TRUE then
    if nachricht ≠ wNew and nachricht ≠ wCalcRgns then
      put checkColor(theWindow) into hasColor
    end if
    if nachricht = wDraw then
      if loWord(daten) = 0 then
        draw_Frame varCode,theWindow,daten,hasColor
      end if
      return 0
    else if nachricht = wHit then
      return report_Hits(varCode,theWindow,daten)
    else if nachricht = wCalcRgns then
      calc_Rgns varCode,theWindow,daten
      return 0
    else if nachricht = wNew then
    else if nachricht = wDispose then
    else if nachricht = wGrow then
      grow_Rect varCode,theWindow,daten
      return 0
    else if nachricht = wDrawGIcon then
      grow_Box varCode,theWindow,daten,hasColor
      return 0
    end if
  else
    return 0
  end if
end dialogWDEF

on draw_Frame varCode,winPtr,daten,hasColor
  setColorPort hasColor
  put windowRect(winPtr) into aRect
  insetRect aRect@,-1,-1
  frameRect aRect@
  insetRect aRect@,-5,-5
  frameRect aRect@
  moveTo aRect@.left +1,aRect@.bottom
  lineTo aRect@.right,aRect@.bottom
  lineTo aRect@.right,aRect@.top +1
  if hasColor then
    setColor 52428,52428,52428
  else
    foreColor whiteColor
  end if
  insetRect aRect@,1,1
  frameRect aRect@
  if hasColor then
    setColor 61166,61166,61166
  end if
  insetRect aRect@,1,1
  frameRect aRect@
  insetRect aRect@,1,1
  frameRect aRect@
  if hasColor then
    setColor 30583,30583,30583
  else
    foreColor blackColor
  end if
  insetRect aRect@,1,1
  frameRect aRect@
  if hasColor then
    setColor 0,0,0
  end if
  disposPtr aRect
end draw_Frame

on calc_Rgns varCode,winPtr,daten
  put windowRect(winPtr) into aRect
  rectRgn winPtr@.contRgn,aRect@
  insetRect aRect@,-6,-6
  rectRgn winPtr@.strucRgn,aRect@
  put newRgn() into shadowRgn
  offsetRect aRect@,1,1
  rectRgn shadowRgn,aRect@
  unionRgn shadowRgn,winPtr@.strucRgn,winPtr@.strucRgn
  disposeRgn shadowRgn
  disposPtr aRect
end calc_Rgns

function report_Hits varCode,winPtr,daten
  put windowRect(winPtr) into aRect
  put newPtr(8) into growIconRect
  insetRect aRect@,-1,-1
  put aRect@.right -kGrowBoxSize into growIconRect@.left
  put aRect@.bottom -kGrowBoxSize into growIconRect@.top
  put aRect@.right into growIconRect@.right
  put aRect@.bottom into growIconRect@.bottom
  if ptInRect(daten,growIconRect@) and bitAnd(varCode,4) = 0 and ¬
  winPtr@.hilited ≠ FALSE then
    put wInGrow into retVal
  else if ptInRect(daten,aRect@) then
    put wInContent into retVal
  else
    put wInDrag into retVal
  end if
  disposPtr growIconRect
  disposPtr aRect
  return retVal
end report_Hits

on grow_Box varCode,winPtr,daten,hasColor
  setColorPort hasColor
  put newRgn() into svClip
  getClip svClip
  put windowRect(winPtr) into aRect
  insetRect aRect@,-1,-1
  put newPtr(8) into growIconRect
  put aRect@.right -kGrowBoxSize into growIconRect@.left
  put aRect@.bottom -kGrowBoxSize into growIconRect@.top
  put aRect@.right into growIconRect@.right
  put aRect@.bottom into growIconRect@.bottom
  put newRgn() into myClip
  copyRgn winPtr@.visRgn,myClip
  offsetRgn myClip,aRect@.left,aRect@.top
  setClip myClip
  disposeRgn myClip
  if hasColor and winPtr@.hilited ≠ FALSE then
    setColor 61166,61166,61166
    paintRect growIconRect@
    setColor 0,0,0
  else
    eraseRect growIconRect@
  end if
  if winPtr@.hilited = FALSE then
    setColor 32767,32767,32767
  end if
  frameRect growIconRect@
  moveTo aRect@.left,growIconRect@.top
  lineTo aRect@.right -1,growIconRect@.top
  moveTo growIconRect@.left,aRect@.top
  lineTo growIconRect@.left,aRect@.bottom -1
  if winPtr@.hilited = FALSE then
    setColor 0,0,0
  else
    put newPtr(8) into giRect
    put growIconRect@.left +4 into giRect@.left
    put growIconRect@.top +4 into giRect@.top
    put growIconRect@.right -3 into giRect@.right
    put growIconRect@.bottom -3 into giRect@.bottom
    if hasColor then
      frameRect giRect@
      put newPtr(8) into clRect
      put giRect@.longIntType[1] into clRect@.longIntType[1]
      put giRect@.longIntType[2] into clRect@.longIntType[2]
      add 1 to clRect@.left
      add 1 to clRect@.top
      setColor 52428,52428,52428
      paintRect clRect@
      add 1 to clRect@.left
      add 1 to clRect@.top
      setColor 0,0,0
      frameRect clRect@
      subtract 1 from clRect@.right
      subtract 1 from clRect@.bottom
      setColor 43690,43690,43690
      paintRect clRect@
      setColor 0,0,0
    else
      eraseRect giRect@
      frameRect giRect@
    end if
    offsetRect giRect@,-3,-3
    add 2 to giRect@.left
    add 2 to giRect@.top
    subtract 1 from giRect@.right
    subtract 1 from giRect@.bottom
    if hasColor then
      frameRect giRect@
      put giRect@.longIntType[1] into clRect@.longIntType[1]
      put giRect@.longIntType[2] into clRect@.longIntType[2]
      add 1 to clRect@.left
      add 1 to clRect@.top
      setColor 52428,52428,52428
      paintRect clRect@
      add 1 to clRect@.left
      add 1 to clRect@.top
      setColor 0,0,0
      frameRect clRect@
      subtract 1 from clRect@.right
      subtract 1 from clRect@.bottom
      setColor 48059,48059,48059
      paintRect clRect@
      disposPtr clRect
      setColor 0,0,0
    else
      eraseRect giRect@
      frameRect giRect@
    end if
    disposPtr giRect
  end if
  disposPtr growIconRect
  disposPtr aRect
  setClip svClip
  disposeRgn svClip
end grow_Box

on grow_Rect varCode,winPtr,daten
  GetWMgrPort bPort
  setPort bPort
  put newPtr(8) into growRect
  put newPtr(8) into growIconRect
  put daten@.longIntType[1] into growRect@.longIntType[1]
  put daten@.longIntType[2] into growRect@.longIntType[2]
  insetRect growRect@,-6,-6
  frameRect growRect@
  insetRect growRect@,5,5
  frameRect growRect@
  moveTo growRect@.left +1,growRect@.top +titleBarHeight
  lineTo growRect@.right -1,growRect@.top +titleBarHeight
  moveTo growRect@.left +1,growRect@.bottom -kGrowBoxSize
  lineTo growRect@.right -2,growRect@.bottom -kGrowBoxSize
  moveTo growRect@.right -kGrowBoxSize,growRect@.bottom -2
  lineTo growRect@.right -kGrowBoxSize,growRect@.top +titleBarHeight +1
  disposPtr growRect
  disposPtr growIconRect
end grow_Rect

function windowRect winPtr
  getPort aPort
  setPort winPtr
  put newPtr(8) into aRect
  put winPtr@.portRect.longIntType[1] into aRect@.longIntType[1]
  localToGlobal aRect@.longIntType[1]
  put winPtr@.portRect.longIntType[2] into aRect@.longIntType[2]
  localToGlobal aRect@.longIntType[2]
  setPort aPort
  return aRect
end windowRect

on setColor rot,gruen,blau
  put newPtr(6) into theColor
  put rot into theColor@.integerType[1]
  put gruen into theColor@.integerType[2]
  put blau into theColor@.integerType[3]
  RGBForeColor theColor@
  disposPtr theColor
end setColor

on setColorPort hasColor
  if hasColor = TRUE then
    GetCWMgrPort aPort
    setPort aPort
  else
    GetWMgrPort bPort
    setPort bPort
  end if
end setColorPort

function checkColor winPtr
  put gestalt("qdrw",theVal) into theErr
  put (bitAnd(theVal,1) ≠ 0) into hasColor
  put GetMaxDevice(winPtr@.strucRgn@@.rgnBBox) into aDevice
  put (hasColor and (aDevice@@.gdPMap@@.pixelSize ≥ 4)) into hasColor
  return hasColor
end checkColor